// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // set to 0 if selector is zx is on
    Mux16 (a=x, sel=zx, b=false, out=appliedzx);

    // zero the y input?
    Mux16 (a=y, sel=zy, b=false, out=appliedzy);

    // flip the bits
    Not16 (in=appliedzx, out=notx);
    Mux16(a=appliedzx, b=notx, sel=nx, out=appliednx);
    Not16 (in=appliedzy, out=noty);
    Mux16(a=appliedzy, b=noty, sel=ny, out=appliedny);

    // aggregate
    Add16(a=appliednx, b=appliedny, out=xplusy);
    And16(a=appliednx, b=appliedny, out=xandy);
    Mux16(b=xplusy, a=xandy, sel=f, out=aggregate);

    // no
    Not16 (in=aggregate, out=notaggregate);
    // if leading zero is 1, then the sum is negative, else positive
    Mux16 (a=aggregate, b=notaggregate, sel=no, out=out, out[0..7]=firsthalf, out[8..15]=secondhalf, out[15]=ng);

    // is it zero?
    Or8Way (in=firsthalf, out=isnotzerofirsthalf);
    Or8Way (in=secondhalf, out=isnotzerosecondhalf);
    Or (a=isnotzerofirsthalf, b=isnotzerosecondhalf, out=isnotzero);
    Not(in=isnotzero, out=zr);
}